buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'

    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    lintOptions {
        checkReleaseBuilds true //TODO There is an weird list error in using deprecated APIs in LocationService.
        abortOnError false
    }
    defaultConfig {
        applicationId "eu.wonderfulme.triptracker"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        resValue("string", "GOOGLE_MAPS_API_KEY", GOOGLE_MAPS_API_KEY) // Add api key to gradle.properties.
    }
    signingConfigs {
        release {
            if (project.hasProperty('MYAPP_RELEASE_STORE_FILE')) { // Save Keystore file under App Module
                storeFile file(MYAPP_RELEASE_STORE_FILE) // Add needed info to gradle.properties.
                storePassword MYAPP_RELEASE_STORE_PASSWORD
                keyAlias MYAPP_RELEASE_KEY_ALIAS
                keyPassword MYAPP_RELEASE_KEY_PASSWORD
            } else {
                throw new GradleException("KeyStore is not available")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    def room_version = "1.1.1"
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    def support_sdk_version = "27.1.1"
    implementation "com.android.support:design:$support_sdk_version"
    implementation "com.android.support:customtabs:$support_sdk_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.android.support:appcompat-v7:$support_sdk_version"
    implementation "com.android.support:preference-v7:$support_sdk_version"
    def play_services_version = "15.0.1"
    implementation "com.google.android.gms:play-services-location:$play_services_version"
    implementation "com.google.android.gms:play-services-maps:$play_services_version"
    implementation "com.google.android.gms:play-services-ads:$play_services_version"
    def butterKnife_version = "8.7.0"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife_version"
    implementation "com.jakewharton:butterknife:$butterKnife_version"

    implementation 'com.opencsv:opencsv:3.9'

    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'
    def work_version = "1.0.0-alpha05"
    implementation "android.arch.work:work-runtime:$work_version"

}

apply plugin: 'com.google.gms.google-services'
